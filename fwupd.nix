{
  config,
  lib,
  pkgs,
  ...
}:

let
  fwupd-notify = pkgs.writeShellScript "fwupd-notify" ''
    set -euo pipefail

    export LANG=C.UTF-8
    export LC_ALL=C

    # Check for updates (no refresh as user, requires root)
    updates=$(/run/current-system/sw/bin/fwupdmgr get-updates 2>/dev/null || true)

    if echo "$updates" | grep -q "No upgrades"; then
      exit 0
    fi

    # If we found updates, send notification
    if echo "$updates" | grep -q "â”‚"; then
      ${pkgs.libnotify}/bin/notify-send \
        -u normal \
        -i system-software-update \
        "Firmware Updates Available" \
        "Run 'fwupdmgr update' to install firmware updates. Note that TPM PCRs will be affected so automatic disk encryption will be reset."
    fi
  '';
in
{
  services.fwupd.enable = true;

  # User service for notifications
  home-manager.sharedModules = [
    {
      systemd.user.services.fwupd-notify = {
        Unit = {
          Description = "Check for firmware updates and notify";
        };
        Service = {
          Type = "oneshot";
          ExecStart = "${fwupd-notify}";
        };
      };

      systemd.user.timers.fwupd-notify = {
        Unit = {
          Description = "Check for firmware updates daily";
        };
        Timer = {
          OnBootSec = "15m";
          OnUnitActiveSec = "1d";
          Persistent = true;
        };
        Install = {
          WantedBy = [ "timers.target" ];
        };
      };
    }
  ];
}
