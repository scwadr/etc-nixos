{
  config,
  lib,
  pkgs,
  ...
}:
{
  imports = [
    ./home-manager.nix
  ];

  options.kiyurica.lsps = lib.mkOption {
    type =
      with lib;
      with types;
      listOf (submodule {
        options.package = mkOption { type = package; };
        options.exec-name = mkOption { type = str; };
      });
    description = "Paths to run and the package of LSP servers. These will be wrapped to only run when the dev sandbox is detected via an env var.";
    default = [ ];
  };
  config =
    let
      wrapLsp =
        { package, exec-name }:
        pkgs.writeShellScriptBin "${package.name}-wrapped" ''
          if [[ -z "$KIYURICA_IN_SANDBOX_DEV" ]]; then
            echo '$0 should be run in the sandbox. Set $KIYURICA_IN_SANDBOX_DEV to a nonempty value to bypass.'
            exit 39
          fi

          exec ${package.outPath}/bin/${exec-name}
        '';
    in
    {
      kiyurica.home-manager.enable = true;
      users.users.kiyurica.packages = with pkgs; [ helix ] ++ builtins.map wrapLsp config.kiyurica.lsps;
      environment.variables.editor = lib.mkOverride 900 "hx";
      home-manager.users.kiyurica =
        { ... }:
        {
          programs.helix = {
            enable = true;
            defaultEditor = true;
            settings = {
              theme = "base16_transparent";
              editor.line-number = "relative";
            };
          };
        };
    };
}
